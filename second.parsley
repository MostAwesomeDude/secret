number = spaces
               ('-' barenumber:x -> t.Exactly(-x)
                    |barenumber:x -> t.Exactly(x))
barenumber = '0' (('x'|'X') <hexdigit+>:hs -> int(hs, 16)
                    |<octaldigit+>:ds -> int(ds, 8))
               |<digit+>:ds -> int(ds)
octaldigit = :x ?(x in '01234567' ) -> x
hexdigit = :x ?(x in '0123456789ABCDEFabcdef') -> x

escapedChar = '\\' ('n' -> "\n"
                     |'r' -> "\r"
                     |'t' -> "\t"
                     |'b' -> "\b"
                     |'f' -> "\f"
                     |'"' -> '"'
                     |'\'' -> "'"
                     |'\\' -> "\\")

character = token("'") (escapedChar | anything):c token("'") -> t.Exactly(c)

string = token('"') (escapedChar | ~('"') anything)*:c
         token('"') -> t.Exactly(''.join(c))

name = <letter letterOrDigit*>

application = name:name
                  ('(' !(self.applicationArgs(finalChar=')')):args ')'
                    -> t.Apply(name, self.rulename, args)
                  | -> t.Apply(name, self.rulename, []))

expr1 = application
          |ruleValue
          |semanticPredicate
          |semanticAction
          |number
          |character
          |string
          |token('(') expr:e token(')') -> e
          |token('<') expr:e token('>') -> t.ConsumedBy(e)
          |token('[') expr:e token(']') -> t.List(e)

expr2 = (token('~') (token('~') expr2:e -> t.Lookahead(e)
                       |expr2:e -> t.Not(e)
          )
          |expr1)

repeatTimes = (barenumber:x -> int(x)) | name

expr3 = (expr2:e
                      ('*' -> t.Many(e)
                      |'+' -> t.Many1(e)
                      |'?' -> t.Optional(e)
                      |'{' spaces repeatTimes:start spaces (
                      (',' spaces repeatTimes:end spaces '}'
                           -> t.Repeat(start, end, e))
                         | spaces '}'
                           -> t.Repeat(start, start, e))
                      | -> e
)):r
           (':' name:n -> t.Bind(n, r)
           | -> r)
          |token(':') name:n
           -> t.Bind(n, t.Apply("anything", self.rulename, []))

expr4 = expr3*:es -> t.And(es)

expr = expr4:e (token('|') expr4)*:es -> t.Or([e] + es)

ruleValue = token("->") -> self.ruleValueExpr(True)

semanticPredicate = token("?(") -> self.semanticPredicateExpr()

semanticAction = token("!(") -> self.semanticActionExpr()

ruleEnd = newline+ | end

rulePart :requiredName = name:n ?(n == requiredName)
                            !(setattr(self, "rulename", n))
                            expr4:args
                            (token("=") expr:e ruleEnd
                               -> t.And([args, e])
                            | ruleEnd -> args)

rule = ~~(name:n) rulePart(n):r
          (rulePart(n)+:rs -> t.Rule(n, t.Or([r] + rs))
          |                -> t.Rule(n, r))

# The "grammar" keyword and suite.
# grammar Ones:
#  ones = '1'+
grammar_stmt = keyword("grammar") identifier_:name colon rule_suite:s
             -> t.Grammar(name, s)
rule_suite = newline indent rule:r rule_stmt*:rs dedent -> [r] + rs
rule_stmt = indentation ~leading_space rule

# Override compound_stmt from the parent grammar to get grammar_stmt into the
# list of valid compound statements.
compound_stmt = grammar_stmt
              | if_stmt
              | while_stmt
              | for_stmt
              | try_stmt
              | with_stmt
              | funcdef
              | classdef
              | decorated
